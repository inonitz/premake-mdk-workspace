#version 450 core
layout(local_size_x =   5, local_size_y =   5, local_size_z =   5) in;
layout (binding = 0)                    uniform sampler2D quantityField;
layout (binding = 1)                    uniform sampler2D velocityField;
layout (binding = 2, rgba32f) writeonly uniform image2D   outputField;


vec4 textureBiLerp(in sampler2D tex, in vec2 uv);
vec2 advection_step(
    in ivec2     coord,
    in vec2      normcoord, 
    in sampler2D ufield, 
    in sampler2D quantity_read
);


uniform float ku_dt;
uniform ivec2 ku_simdims;
uniform vec2  ku_simUnitCoord;
vec2 k_winUnitCoord = vec2(1) / vec2(ku_simdims); 


void main()
{
    ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);
    vec2  result = advection_step(
        texelCoord,
        k_winUnitCoord * vec2(texelCoord),
        velocityField,
        quantityField
    );
    imageStore(outputField, texelCoord, vec4(result, 0.0f, 0.0f));
    return;
}



/* 
    There is something wrong with the advection shader,
    little 'line' artifacts - maybe it has to do with texture offsets
*/
vec2 advection_step(
    in ivec2     coord,
    in vec2      normcoord, 
    in sampler2D ufield, 
    in sampler2D quantity_read
) {
    vec2 newcoord = normcoord - ku_dt * ku_simUnitCoord * texelFetch(ufield, coord, 0).xy;
    return textureBiLerp(quantity_read, newcoord).xy;
}




vec4 textureBiLerp(in sampler2D tex, in vec2 uv) {
    vec2 texBufferCoord = uv * vec2(ku_simdims);
    vec2 texelCoord = floor(texBufferCoord);
    vec2 weight = texBufferCoord - texelCoord;

    vec4 bottom = mix(
        texelFetch(tex, ivec2(texelCoord) + ivec2(0, 0), 0), 
        texelFetch(tex, ivec2(texelCoord) + ivec2(1, 0), 0),
        weight.x
    );
    vec4 top    = mix(
        texelFetch(tex, ivec2(texelCoord) + ivec2(0, 1), 0), 
        texelFetch(tex, ivec2(texelCoord) + ivec2(1, 1), 0),
        weight.x
    );
    return mix(bottom, top, weight.y);
}